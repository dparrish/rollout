#!/usr/bin/perl -w
# vim:tw=100 sw=2 expandtab ft=perl
=head1 NAME

920-rsyncd

=head1 DESCRIPTION

Set up /etc/rsyncd.conf to allow rsync file transfers.

B<NOTE:> This uses the rsync protocol, which is less secure than using rsync over SSH.

=head1 EXAMPLE

  class Sample => {
    rsync => {
      testmodule => {
        comment => "Test module",
        path => "/tmp",
        user => "foouser",
        password => "foopass",
        allow => '10.0.0.0/24',
        chroot => 1,
        uid => 0,
        gid => 0,
        readonly => 1,
      },
    },
  };

=head1 COPYRIGHT

Copyright 2010 David Parrish

=cut

validate_config {
  rsync => {
    type => "hash",
    key => {
      help => "Module name (see rsyncd.conf(5))",
      type => "string",
    },
    value => {
      type => "options",
      options => {
        allow => { type => "string", help => "List of hosts to allow access" },
        chroot => { type => "string" },
        comment => { type => "string" },
        gid => { type => "string" },
        password => { type => "string", help => "Auth password" },
        path => { type => "string" },
        readonly => { type => "string" },
        uid => { type => "string" },
        user => { type => "string", help => "Auth user" },
      },
    },
  },
};

return unless my %rsync = flatten_hash(c("$hostname/rsync"));
package_check("rsync");

if (-f '/etc/xinetd.d/rsync') {
  file_modify('/etc/xinetd.d/rsync', '/etc/init.d/xinetd reload',
              's/disable\s*=\s*yes/disable = no/');
} elsif (-f '/etc/default/rsync') {
  eval {
  file_modify('/etc/default/rsync', '/etc/init.d/rsync restart',
              's/RSYNC_ENABLE=false/RSYNC_ENABLE=true/');
          };
          print $@;
}

my %users;
my $config = "";

while (my($name, $r) = each %rsync) {
  validate("No path specified for rsync module \"$name\"", $r->{path});
  $r->{uid} ||= 'root';
  $r->{user} = 'backup' unless defined $r->{user};
  $config .= "[$name]\n";
  $config .= "  comment = $r->{comment}\n" if $r->{comment};
  $config .= "  path = $r->{path}\n";
  $config .= "  auth users = $r->{user}\n" if $r->{user};
  $config .= "  hosts allow = $r->{allow}\n" if $r->{allow};
  $config .= "  secrets file = /etc/rsync.secrets\n" if $r->{user};
  $config .= "  uid = $r->{uid}\n" if $r->{uid};
  $config .= "  gid = $r->{gid}\n" if $r->{gid};
  $config .= "  use chroot = $r->{chroot}\n" if $r->{chroot};
  $config .= "  read only = yes\n" unless $r->{readonly};
  $config .= "\n";

  $users{$r->{user}} = $r->{password} if $r->{user} && $r->{password};
}

my $restart = 0;
text_install("/etc/rsyncd.conf", $config) and $restart++ if $config;
text_install("/etc/rsync.secrets", join("", map { "$_:$users{$_}\n" } keys %users), undef,
             { mode => 0600 }) and $restart++ if keys %users;
command("/etc/init.d/rsync restart") if -f '/etc/init.d/rsync' && $restart;

