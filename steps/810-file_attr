#!/usr/bin/perl -w
# vim:tw=100 sw=2 expandtab ft=perl
=head1 NAME

810-file_attr

=head1 DESCRIPTION

Set attributes (owner, group, mode) for files.

=head1 EXAMPLE

  class Sample => {
    file_attr => {
      '/tmp/foobar' => 0644,
      '/tmp/foobar2' => { mode => 0644, uid => 0, gid => 0 },
    },
  };

=head1 COPYRIGHT

RolloutNG is based heavily on Rollout (https://github.com/dparrish/rollout) by David Parrish, Copyright 2010.   RolloutNG is Copyright by Chris Donovan 2014.

=cut

validate_config {
  file_attr => {
    type => "hash",
    key => {
      type => "path",
    },
    value => {
      type => [ "int", "options" ],
      help => [ "Mode", "Hash of attributes" ],
      options => {
        gid => { type => "int", range => [0, 65535] },
        group => { type => "string", regex => qr/^[a-z0-9_\-.]+$/ },
        mode => { type => "int", range => [0, 07777] },
        owner => { type => "string", regex => qr/^[a-z0-9_\-.]+$/ },
        uid => { type => "int", range => [0, 65535] },
      },
    },
  },
};

my %attr = flatten_hash(c("$hostname/file_attr"));
while (my($file, $attr) = each %attr) {
  $attr = { -mode => $attr } unless ref $attr eq 'HASH';
  $attr->{-file} = $file;
  $attr = { map { (/^-/ ? $_ : "-$_") => $attr->{$_} } keys %$attr };
  set_attr(%$attr);
}

