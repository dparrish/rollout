#!/usr/bin/perl -w
# vim:tw=100 sw=2 expandtab ft=perl
=head1 NAME

140-pre_pkg_file_install

=head1 DESCRIPTION

Install files before packages

=head1 EXAMPLE

  class Sample => {
    pre_pkg_file_install => {
      '/tmp/dest_file' => {
        source => 'rollout:/conf/source_file',
        command => 'ls -l /tmp/dest_file',
        mode => 0640,
        uid => 1000,
        gid => 1000,
      },
      '/tmp/dest_file' => { text => 'This is some text\n' },
    },
  };

=head1 SEE ALSO

L<250-file_install>

=head1 COPYRIGHT

Copyright 2010 David Parrish

=cut

validate_config {
  pre_pkg_file_install => {
    type => "hash",
    key => {
      help => "Destination path",
      type => "path",
    },
    value => {
      type => "options",
      options => {
        command => { type => "string" },
        gid => { type => "int", range => [0, 65535] },
        group => { type => "string", regex => qr/^[a-z0-9_\-.]+$/ },
        mode => { type => "int", range => [0, 07777] },
        owner => { type => "string", regex => qr/^[a-z0-9_\-.]+$/ },
        source => { type => "string" },
        text => { type => "string" },
        uid => { type => "int", range => [0, 65535] },
      },
    },
  },
};

my %done_files;
my %files = flatten_hash(c("$hostname/pre_pkg_file_install"));
while (my($dest, $f) = each(%files)) {
  next if $done_files{$dest}++;
  next unless i_should($dest);
  if ($f->{source}) {
    file_install(-file => $dest, -src => $f->{source}, -cmd => $f->{command} || undef,
                 -mode => $f->{mode}, -uid => $f->{uid}, -gid => $f->{gid});
  } elsif ($f->{text}) {
    if ($f->{text} =~ /^\n?(\s+)/m) {
      my $strip = $1;
      v "Stripping \"$strip\" from the start of text for the file: $dest";
      my @text = split(/\n/, $f->{text});
      $f->{text} = "";
      foreach (@text) { s/^$strip//; $f->{text} .= "$_\n"; };
    }
    text_install(-file => $dest, -text => $f->{text}, -cmd => $f->{command} || undef,
                 -mode => $f->{mode}, -uid => $f->{uid}, -gid => $f->{gid});
  }
}
