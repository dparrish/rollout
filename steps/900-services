#!/usr/bin/perl -w
# vim:tw=100 sw=2 expandtab ft=perl
=head1 NAME

900-services

=head1 DESCRIPTION

Start or stop services and configure them to start or stop on boot.

By default this will run startup scripts in /etc/init.d. This can be changed using the B<check>
option.

The value of B<check> can be either:

=over

=item

A shell commandline which exits with 0 if the job is running or 1 otherwise.

=item

An anonymous subroutine which returns true or false depending on whether the job is running or not.

=back

=head1 EXAMPLE

  class Sample => {
    service => {
      # Run httpd
      httpd => 1,
      # Run snmpd. The default snmp script checks the status of snmptrapd too, which we don't want
      # to run.
      snmp => {
        status => 1,
        proc => 'snmpd',
      },
      # Run rollout, with custom checks
      rollout => {
        status => 1,
        check => "ps auxww | grep -q 'perl .*rolloutd'",
      },
      # Always return "not running" for snmptrapd, and don't try to run it
      snmptrapd => {
        status => 0,
        check => sub { return 0 }.
      },
      # Don't run postfix
      postfix => 0,
    },
  };

=head1 COPYRIGHT

RolloutNG is based heavily on Rollout (https://github.com/dparrish/rollout) by David Parrish, Copyright 2010.   RolloutNG is Copyright by Chris Donovan 2014.

=cut

use IO::Dir;

validate_config {
  service => {
    type => "hash",
    key => {
      help => "Service name (and startup script in /etc/init.d)",
      type => "string",
    },
    value => {
      type => [ "boolean", "options" ],
      help => [ "Run service?", "Service options" ],
      options => {
        proc => { type => "string", help => "Process name" },
        status => { type => "boolean", help => "Run service?" },
        check => { type => [ "string", "code" ], help => "Script to check process status" },
      },
    },
  },
};

my %services = flatten_hash(c("$hostname/service"));
while (my($service, $s) = each(%services)) {
  v "Checking service $service";
  my $processname;
  my $status = $s;
  my $running = 0;
  my $check = undef;

  if (ref $s eq 'HASH') {
    $processname = $s->{proc} || undef;
    $status = $s->{status} || 0;
    $check = $s->{check} || undef;
  }

  if ($check) {
    if (ref $check eq 'CODE') {
      v "Running custom check code";
      $running = $check->();
    } else {
      v "Running check $check";
      system("$check >/dev/null 2>/dev/null");
      $running = !$?;
    }
  } elsif ($processname) {
    my %dir;
    tie %dir, 'IO::Dir', "/proc";
    foreach (grep /^\d+$/, keys %dir) {
      my $exe = readlink("/proc/$_/exe") || "";
      $running = 1 if $exe =~ /\/$processname$/;
    }
    untie %dir;
  } elsif (-x "/usr/sbin/invoke-rc.d") {
    system("/usr/sbin/invoke-rc.d $service status >/dev/null 2>/dev/null");
    $running = ($? == 0);
  } elsif (-f "/etc/init.d/$service") {
    system("/etc/init.d/$service status >/dev/null 2>/dev/null");
    $running = ($? == 0);
  }

  v "Status is $running, should be $status";
  if (-f "/sbin/chkconfig") {
    if ($status) {
      # Make sure service is running
      command("chkconfig", $service, "on") unless $running;
      command("service", $service, "start") unless $running;
    } else {
      # Make sure service is stopped
      command("chkconfig", $service, "off") unless $running;
      command("service", $service, "stop") if $running;
    }
  } elsif (-f "/usr/sbin/update-rc.d") {
    if ($status) {
      # Make sure service is running
      command("update-rc.d", $service, "defaults") unless $running;
      command("invoke-rc.d", $service, "start") unless $running;
    } else {
      # Make sure service is stopped
      command("update-rc.d", $service, "remove") if $running;
      command("invoke-rc.d", $service, "stop") if $running;
    }
  } else {
    w "Don't know how to control services";
  }
}
