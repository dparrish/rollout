#!/usr/bin/perl -w
# vim:tw=100 sw=2 expandtab ft=perl
=head1 NAME

600-sudo

=head1 DESCRIPTION

Add user and command entries to /etc/sudoers.

=head1 EXAMPLE

  class Sample => {
    sudo => {
      username => [ "/bin/ls", "/usr/local/bin/rollout" ],
      '%admin' => [ "/usr/local/bin/rollout" ],
      '%wheel' => [ "ALL" ],
    },
  };

=head1 OPTIONS

=over

=item wheel_all

Automatically allow the C<%wheel> group to run all commands

=item I<username>

Add this user to sudoers

=back

=head1 COPYRIGHT

RolloutNG is based heavily on Rollout (https://github.com/dparrish/rollout) by David Parrish, Copyright 2010.   RolloutNG is Copyright by Chris Donovan 2014.

=cut


validate_config {
  sudo => {
    type => "hash",
    key => {
      help => "Username or group name (prepended with %)",
      type => "string",
    },
    value => {
      help => "List of commands to allow",
      type => "list",
      items => { type => [ "path", "string" ], regex => qr/^ALL$/ },
    },
  },
};


my %sudo;
my %input = flatten_hash(c("$hostname/sudo"));
while(my($username, $commands) = each(%input)) {
  next unless i_should($username);
  map { $sudo{$username}->{$_}++ } @$commands;
}

return unless %sudo;

my $text = <<EOF;
# sudoers file.
#
# This file is rebuilt by rollout, so any changes made locally will be lost
# next time rollout is run.

# Root can run anything as any other user
root   ALL=(ALL) ALL

Defaults syslog=auth

EOF

if (i_should("wheel_all")) {
  $text .= <<EOF;
# Allow people in group wheel to run all commands, but still require password
\%wheel ALL=(ALL) ALL

EOF
}
$text .= "$_ ALL=(ALL) ". join(", ", keys %{$sudo{$_}}). "\n"
  foreach keys %sudo;

text_install(-file => "/etc/sudoers", -text => $text, -cmd => "visudo -c",
             -mode => 0440, -uid => 0, -gid => 0);

