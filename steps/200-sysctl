#!/usr/bin/perl -w
# vim:tw=100 sw=2 expandtab ft=perl
=head1 NAME

200-sysctl

=head1 DESCRIPTION

Add entries to /etc/sysctl.conf

=head1 EXAMPLE

  class Sample => {
    sysctl => {
      'net.ipv4.tcp_syncookies' => 1,
      'net.ipv4.conf.all.accept_source_route' => 0,
    },
  };

=head1 OPTIONS

=over

=item sysctl

Add or replace the given entries in /etc/sysctl.conf. After adding, they will be run.

=back

=head1 COPYRIGHT

RolloutNG is based heavily on Rollout (https://github.com/dparrish/rollout) by David Parrish, Copyright 2010.   RolloutNG is Copyright by Chris Donovan 2014.

=cut


validate_config {
  sysctl => {
    type => "hash",
    key => {
      help => "Sysctl name",
      type => "string",
      regex => qr/^[\w_\-]+.[\w_\-.]+$/,
    },
    value => {
      help => "Sysctl value",
      type => "string",
    },
  },
};

my $changed = 0;
my %sysctl = flatten_hash(c("$hostname/sysctl"));
while (my($item, $value) = each(%sysctl)) {
  my $text = i_os("freebsd") ? "$item=$value" : "$item = $value";
  file_append(-file => "/etc/sysctl.conf", -add => $text, -match => qr/^#?\s*$item/,
              -uid => 0, -gid => 0) and $changed++;
}

if ($changed) {
  if (i_os("linux")) {
    command("sysctl", "-p", "/etc/sysctl.conf");
  } elsif (i_os("freebsd")) {
    command("/etc/rc.d/sysctl", "restart");
  } else {
    throw ConfigException "Don't know how to reload sysctl entries on ". i_os();
  }
}
