#!/usr/bin/perl -w
# vim:tw=100 sw=2 expandtab ft=perl
=head1 NAME

220-iscsi

=head1 DESCRIPTION

Configure iSCSI targets.

Currently works only on Debian / Ubuntu.

=head1 EXAMPLE

  $m{Sample} = {
    iscsi => {
      startup => 1,
      initiator => "initiatorname",
      targets => {
        footarget => '192.168.1.5',
        bartarget => '192.168.1.6:3260',
      },
    }
  };

=head1 COPYRIGHT

Copyright 2010 David Parrish

=cut

validate_config {
  iscsi => {
    type => "options",
    options => {
      startup => { type => "boolean" },
      initiator => { type => "string" },
      targets => {
        type => "hash",
        key => { type => "string" },
        value => { type => "string" },
      },
    },
  },
};

return unless my $i = c("$hostname/iscsi");

my $restart = 0;
my $auto = "automatic";
if (i_distro("debian", "ubuntu")) {
  package_check("open-iscsi") && $restart++;;
}
else {
  return w "Don't know how to manage iSCSI on ". i_os(). " ". i_distro();
}

$auto = $i->{startup} if $i->{startup};

if ($i->{initiator}) {
  # Force ISCSI initiator
  if (file_modify("/etc/initiatorname.iscsi", undef,
                  "s/^InitiatorName=\\S+\$/InitiatorName=$i->{initiator}/")) {
    w "Forced ISCSI initiator name to be \"$i->{initiator}\" - check initiator security";
    command("/etc/init.d/open-iscsi", "restart");
  }
}

if ($i->{targets}) {
  while (my($target, $ip) = each(%{$i->{targets}})) {
    $ip .= ":3260" unless $ip =~ /:\d+$/;

    my $filename = "/etc/iscsi/send_targets/$ip";
    $filename =~ s/:(\d+)$/,$1/;

    if (!-f $filename) {
      v "Discovering targets on $ip ($filename missing)";
      command("iscsiadm", "-m", "discovery", "-t", "sendtargets", "-p", $ip);
    }

    if (!-d "/etc/iscsi/nodes/$target") {
      v "Connecting to node $target";
      command("iscsiadm", "-m", "node", "-T", $target, "-p", $ip, "--op", "update",
              "-n", "node.conn[0].startup", "-v", "automatic");
    }

    $filename = "/etc/iscsi/nodes/$target/$ip";
    $filename =~ s/:(\d+)$/,$1/;

    file_modify($filename, undef,
                's/^node.startup\s*=.*/node.startup = automatic/',
                's/^node.conn\[\d+\].startup\s*=.*/node.conn[0].startup = automatic/',
    ) && $restart++;
  }
}

if ($auto) {
  # Configure whether to connect to nodes on startup
  file_modify("/etc/iscsid.conf", undef,
              's/^node.startup\s*=\s*(automatic|manual)/node.startup = '. $auto. '/') && $restart++;
}

if ($restart) {
  v "Restarting ISCSI services";
  command("/etc/init.d/open-iscsi", "restart");
}

