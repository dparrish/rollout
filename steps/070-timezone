#!/usr/bin/perl -w
# vim:tw=100 sw=2 expandtab ft=perl
=head1 NAME

070-timezone

=head1 DESCRIPTION

Set the timezone of the machine.

=head1 EXAMPLE

  $m{Sample} = {
    timezone => "Australia/Sydney",
  };

=head1 OPTIONS

=over

=item timezone

This should be a file or a directory that exists in C</usr/share/zoneinfo> or
C</usr/share/lib/zoneinfo>.

=back

=head1 COPYRIGHT

Copyright 2010 David Parrish

=cut

validate_config {
  timezone => {
    type => "string",
  },
};

return unless i_has("timezone") && i_should("timezone");
my $zone = (flatten_list(c("$hostname/timezone")))[0];
print "No timezone\n" and return unless $zone;

if (i_os("solaris")) {
  validate("Unknown timezone $zone", -f "/usr/share/lib/zoneinfo/$zone");
  file_append("/etc/TIMEZONE", "TZ=\"$zone\"", qr/^TZ=/);
} elsif (i_os("freebsd")) {
  validate("Unknown timezone $zone", -f "/usr/share/zoneinfo/$zone");
  file_install("/etc/localtime", "/usr/share/zoneinfo/$zone");
} else {
  validate("Unknown timezone $zone", -f "/usr/share/zoneinfo/$zone");
  text_install("/etc/timezone", "$zone\n");

  if (-f "/etc/sysconfig/clock") {
    # RedHat specific
    file_append("/etc/sysconfig/clock", "ZONE=\"$zone\"", qr/^ZONE=/);
  }

  if (-e "/etc/localtime" && -f "/usr/share/zoneinfo/$zone" && !$safe_mode) {
    unlink("/etc/localtime") unless -l "/etc/localtime";
    symlink_check("/usr/share/zoneinfo/$zone", "/etc/localtime");
  }
}
