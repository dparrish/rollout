#!/usr/bin/perl -w
# vim:tw=100 sw=2 expandtab ft=perl
=head1 NAME

212-git

=head1 DESCRIPTION

Check out git modules to the local filesystem.

=head1 EXAMPLE

  class Sample => {
    git => {
      '/tmp/checkout_path' => {
        source => 'http://git.domain/module/trunk.git',
        branch => 'master',
        user => 'jenkins',
        clone_options => [],
        pull_options => [],
        cmd => 'ls -l',
      },
    },
  };

=head1 OPTIONS

=over

=item git/I<path>

Check out the given module to path.

=item git/I<path>/source

The git URL that will be cloned

=item git/I<path>/user

The username or UID that will own the newly created directory and all the files underneath it. This
user is the one that will run the git commands.

=item git/I<path>/branch

The branch to check out. If this changes, the current checked out version will be updated to match
the revision specified here.

=item git/I<path>/cmd

A shell command to be run after the checkout or any updates are applied. This command is run from
inside the checked-out directory. This command will be run as root, no matter what the "user" option
specifies.

=item git/I<path>/clone_options

Any options to add to the git clone command line

=item git/I<path>/pull_options

Any options to add to the git pull command line

=item git/I<path>/always_pull

Automatically pull updates. Defaults to true.

=back

=head1 COPYRIGHT

Copyright 2010 David Parrish

=cut

validate_config {
  git => {
    type => "hash",
    key => {
      help => "Destination local path to check out to",
      type => "path",
    },
    value => {
      type => "options",
      options => {
        source => { type => "string", required => 1 },
        user => { type => "string", help => "Username or UID that will own the new directory and files" },
        branch => { type => "string" },
        cmd => { type => ["code", "string"], help => "Post checkout command to run (in checked out dir)" },
        clone_options => { type => "list", items => { type => "string" } },
        pull_options => { type => "list", items => { type => "string" } },
        always_pull => { type => "boolean" },
      },
    },
  },
};

return unless i_has('git');
my %git = flatten_hash(c("$hostname/git"));
package_check("git-core") if keys %git && (c("$hostname/_os_detection/distro") eq 'ubuntu' ||
                                           c("$hostname/_os_detection/distro") eq 'debian');
foreach my $dir (sort(keys %git)) {
  my $x = $git{$dir};
  $x->{always_pull} = 1 unless defined $x->{always_pull};
  if (!-d $dir) {
    # New checkout
    my $basedir = dirname($dir);
    dir_check($basedir);
    chdir $basedir or throw LocalFileException "Could not cd to new dir $basedir: $!";
    v "Checking out $x->{source} to $dir";
    my @cmd = ("git", "clone");
    push @cmd, "--branch", $x->{branch} if $x->{branch};
    push @cmd, @{$x->{clone_options} || []};
    push @cmd, $x->{source}, $dir;
    command(@cmd, { uid => $x->{user} });
    if ($x->{cmd}) {
      chdir $dir or throw LocalFileException "Could not cd to new checked out dir $dir: $!";
      if (ref $x->{cmd} eq 'CODE') {
        l "Running perl code to finish checkout of $dir";
        $x->{cmd}->();
      } else {
        l "Running $x->{cmd} to finish checkout of $dir";
        command($x->{cmd});
      }
    }
  } elsif ($x->{always_pull}) {
    # Update existing checked out directory
    chdir $dir;
    my @cmd = ("git", "pull", "--all", "-q");
    push @cmd, @{$x->{pull_options} || []};
    v "Updating $dir from $x->{source}";
    if ($x->{branch}) {
      my $ret = command(@cmd, { uid => $x->{user} });
      if ($ret != 0) {
        w "Error updating $dir from $x->{source}, not switching branches";
        next;
      }
      command("git", "checkout", $x->{branch}, { uid => $x->{user} })
    }
    if ($x->{cmd}) {
      if (ref $x->{cmd} eq 'CODE') {
        l "Running perl code to finish update of $dir";
        $x->{cmd}->();
      } else {
        l "Running $x->{cmd} to finish update of $dir";
        command($x->{cmd});
      }
    }
  }
}
